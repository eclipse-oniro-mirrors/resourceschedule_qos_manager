# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

config("concurrent_task_config") {
  visibility = [ ":*" ]
  cflags_cc = [ "-fexceptions" ]
  cflags = [
    "-fstack-protector-strong",
    "-Wno-shift-negative-value",
  ]
  include_dirs = [
    "include",
    "../include",
    "../frameworks/concurrent_task_client/include/",
    "../interfaces/inner_api/",
    "../common/include/",
  ]
}

ohos_shared_library("concurrentsvc") {
  public_configs = [ ":concurrent_task_config" ]
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    cfi_no_nvcall = true
    cfi_vcall_ical_only = true
    debug = false
  }

  cflags_cc = [
    "-fomit-frame-pointer",
    "-fdata-sections",
    "-ffunction-sections",
    "-fno-unroll-loops",
    "-fno-exceptions",
    "-fno-unwind-tables",
    "-fno-asynchronous-unwind-tables",
    "-Os",
  ]

  ldflags = [ "-Wl,--exclude-libs=ALL" ]
  sources = [
    "../common/src/concurrent_task_utils.cpp",
    "src/concurrent_task_controller_interface.cpp",
    "src/concurrent_task_service.cpp",
    "src/concurrent_task_service_ability.cpp",
    "src/concurrent_task_service_stub.cpp",
    "src/func_loader.cpp",
    "src/qos_interface.cpp",
    "src/qos_policy.cpp",
  ]

  deps = [ "../etc/param:ffrt_etc" ]

  if (defined(
      global_parts_info.hmosresourceschedule_frame_aware_sched_override)) {
    defines = [ "QOS_EXT_ENABLE" ]
  }

  external_deps = [
    "access_token:libaccesstoken_sdk",
    "c_utils:utils",
    "config_policy:configpolicy_util",
    "frame_aware_sched:rtg_interface",
    "hilog:libhilog",
    "hitrace:hitrace_meter",
    "init:libbegetutil",
    "ipc:ipc_single",
    "jsoncpp:jsoncpp",
    "libxml2:libxml2",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]

  subsystem_name = "resourceschedule"
  part_name = "qos_manager"
}
